import org.gradle.util.GradleVersion
import java.time.Instant

plugins {
    id "fabric-loom" version "0.8-SNAPSHOT"
    id "net.nemerosa.versioning" version "2.15.1"
}

group = "selic.re"
version = "1.1.0"

java {
    withSourcesJar()
}

loom {
    refmapName = "mixins/discord-bridge/refmap.json"
    runs {
        configureEach {
            vmArg("-Dmixin.debug=true")
            vmArg("-Dmixin.debug.export.decompile=false")
            vmArg("-Dmixin.debug.verbose=true")
            vmArg("-Dmixin.dumpTargetOnFailure=true")
            vmArg("-Dmixin.checks=true")
            vmArg("-Dmixin.hotSwap=true")
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://m2.dv8tion.net/releases"
        content {
            includeGroup "net.dv8tion"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.17.1"
    mappings "net.fabricmc:yarn:1.17.1+build.30:v2"
    modImplementation "net.fabricmc:fabric-loader:0.11.6"

    modImplementation include(fabricApi.module("fabric-api-base", "0.37.1+1.17"))
    modImplementation include(fabricApi.module("fabric-lifecycle-events-v1", "0.37.1+1.17"))

    implementation(include("net.dv8tion:JDA:4.3.0_293")) {
        exclude module: "opus-java"
    }

    implementation include("club.minnced:discord-webhooks:0.5.7")

    include "com.neovisionaries:nv-websocket-client:2.14"
    include "com.squareup.okhttp3:okhttp:3.13.0"
    include "com.squareup.okio:okio:1.17.2"
    include "org.apache.commons:commons-collections4:4.1"
    include "org.apache.commons:commons-compress:1.8.1"
    include "net.sf.trove4j:trove4j:3.0.3"
    include "com.fasterxml.jackson.core:jackson-annotations:2.10.1"
    include "com.fasterxml.jackson.core:jackson-core:2.10.1"
    include "com.fasterxml.jackson.core:jackson-databind:2.10.1"
    include "org.json:json:20210307"
}

compileJava {
    options.with {
        it.release.set(16)
        it.fork = true
        it.deprecation = true
        it.encoding = "UTF-8"
        it.compilerArgs.addAll(["-Xlint:all", "-parameters"])
    }
}

processResources {
    filesMatching("/fabric.mod.json") {
        expand("version": project.version)
    }
}

jar {
    from("/LICENSE")
    manifest.attributes(
        "Build-Timestamp": Instant.now(),
        "Build-Revision": versioning.info.commit,
        "Build-Jvm": "" +
            "${System.getProperty("java.version")} (" +
            "${System.getProperty("java.vm.vendor")} " +
            "${System.getProperty("java.vm.version")})",
        "Built-By": GradleVersion.current(),

        "Implementation-Title": project.name,
        "Implementation-Version": project.version,
        "Implementation-Vendor": project.group,

        "Specification-Title": project.name,
        "Specification-Version": "1.0.0",
        "Specification-Vendor": project.group,

        "Sealed": "true"
    )
}
