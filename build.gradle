import org.gradle.util.GradleVersion
import java.time.Instant

plugins {
    id "fabric-loom" version "0.12-SNAPSHOT"
    id "io.github.juuxel.loom-quiltflower" version "1.7.2"
    id "net.nemerosa.versioning" version "2.15.1"
}

group = "selic.re"
version = "1.1.0"

java {
    withSourcesJar()
}

loom {
    mixin {
        defaultRefmapName = "mixins/discord-bridge/refmap.json"
    }
    runs {
        configureEach {
            property "mixin.debug", "true"
            property "mixin.debug.export.decompile", "false"
            property "mixin.debug.verbose", "true"
            property "mixin.dumpTargetOnFailure", "true"
            property "mixin.checks", "true"
            property "mixin.hotSwap", "true"
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://m2.dv8tion.net/releases"
        content {
            includeGroup "net.dv8tion"
        }
    }
}

configurations {
    transitiveInclude
}

dependencies {
    minecraft "com.mojang:minecraft:1.19"
    mappings "net.fabricmc:yarn:1.19+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.14.6"

    modImplementation include(fabricApi.module("fabric-api-base", "0.55.2+1.19"))
    modImplementation include(fabricApi.module("fabric-lifecycle-events-v1", "0.55.2+1.19"))
    modImplementation include(fabricApi.module("fabric-message-api-v1", "0.55.2+1.19"))

    implementation(transitiveInclude("net.dv8tion:JDA:5.0.0-alpha.9")) {
        exclude module: "opus-java"
    }

    implementation transitiveInclude("club.minnced:discord-webhooks:0.8.0")

    project.configurations.transitiveInclude {
        def includer = { self, Set<ResolvedDependency> dependencies -> 
            dependencies.forEach {
                include("${it.moduleGroup}:${it.moduleName}:${it.moduleVersion}")
                self(self, it.children)
            }
        }

        includer(includer, resolvedConfiguration.firstLevelModuleDependencies)
    }

    testImplementation "com.google.truth:truth:1.1.3"
}

compileJava {
    options.with {
        it.release.set(17)
        it.fork = true
        it.deprecation = true
        it.encoding = "UTF-8"
        it.compilerArgs.addAll(["-Xlint:all", "-parameters"])
    }
}

processResources {
    filesMatching("/fabric.mod.json") {
        expand("version": project.version)
    }
}

jar {
    from("/LICENSE")
    manifest.attributes(
        "Build-Timestamp": Instant.now(),
        "Build-Revision": versioning.info.commit,
        "Build-Jvm": "" +
            "${System.getProperty("java.version")} (" +
            "${System.getProperty("java.vm.vendor")} " +
            "${System.getProperty("java.vm.version")})",
        "Built-By": GradleVersion.current(),

        "Implementation-Title": project.name,
        "Implementation-Version": project.version,
        "Implementation-Vendor": project.group,

        "Specification-Title": project.name,
        "Specification-Version": "1.0.0",
        "Specification-Vendor": project.group,

        "Sealed": "true"
    )
}
